init:
    $ el_and_mz = 0
    $ library_visited = False
    $ medical_visited = False
    $ musical_visited = False
    $ clubs_visited = False

label devilishadv_day2_ru:
    $ backdrop = "days"
    $ new_chapter (2, u"Люцифер. День 2")
    play music music_list["into_the_unknown"] fadein 2
    scene island_reverse with fade
    show amen_angry with dspr
    show pi far at left with dspr
    show prologue_dream
    window show
    "Мне снилась какая-то чушь. Высокий чёрный мужчина упрекал меня, что мне пора обратно в Ад.{w} С ним был тот самый пионер из леса, он что-то шептал чёрному на ухо. От этого на него нахлынула ярость, и он бросился на меня.{w} Дрались мы, как мне показалось, целую вечность…"
    window hide
    stop music fadeout 2
    $ persistent.sprite_time = 'day'
    $ day_time()
    scene white with flash
    window show
    "В глаза ударил яркий свет."
    th "Что, опять автобус?"
    play ambience ambience_int_cabin_day
    scene bg int_house_of_mt_day with flash
    play music music_list["lightness"] fadein 4
    "Я приоткрыл глаза, чтобы понять, где я. Сон оставил меня слегка дезориентированным.{w} Лежал я на краю кровати, в спину что-то упиралось."
    play sound sfx_bed_squeak1
    "Я повернулся, увидев на той же кровати мирно посапывающую Олю."
    th "Ах да, наше вчерашнее развлечение. Надеюсь, она не подумает, что из-за этого у нас теперь всё серьезно. Иначе и выселяться из домика придётся. "
    "Встав с кровати, я посмотрел на время. “It’s a quarter to eight” – светилось на экране телефона."
    th "Не пора ли Ольге уже бежать по делам вожатой?"
    "Будить мне её особо не хотелось, ведь не давал ей спать я довольно долго.{w} Hо всё же, её главное желание – соблюдать порядок, и про него я не забыл."
    lur "Олечка…{w} Ольга!{w} Ольга Дмитриевна!"
    "Я прошептал, постепенно повышая тон. Слегка подтолкнув её, она приоткрыла глаза. "
    show mt surprise swim close at right with dissolve
    mt "Лю- Люцифер? Что… Что у нас вчера было?"
    lur "Секс, а что ещё? Какой-то глупый вопрос, если честно. "
    "Похоже, у Ольги перед глазами пронеслись все события вчерашнего вечера. После небольшой паузы, она сказала:"
    show mt normal swim close at right with dspr
    mt "Я могу рассчитывать, что это останется между нами?"
    lur "Конечно, я ведь дьявол своего слова."
    "С игривой улыбкой сказал я."
    show mt angry swim close at right with dspr
    mt "Хорошо. Тогда никому ни слова об этой ночи и впредь называй меня на Вы, пожалуйста. {w}Не знаю, что на меня вчера нашло, но надеюсь, что этого больше не повторится."
    lur "Хорошо, раз для теб- для вас это так важно, никто не узнает об этом."
    "Она посмотрела на будильник, который стоял у её кровати. "
    show mt surprise swim close at right with dspr
    mt "Уже так поздно?! Линейка через 15 минут!"
    hide mt with moveoutleft
    "Она соскочила с кровати и принялась собираться. Уже через каких-то 5 минут она была готова к выходу, пока я лениво застёгивал рубашку и надевал брюки. "
    show mt angry pioneer with dissolve
    mt "Во что это ты одеваешься? Твоя форма у тебя на кровати."
    "Я взглянул на кровать, на которой лежали шорты, пионерская рубашка и красный галстук. Носить я пионерскую форму, естественно, не собирался. "
    "Взяв с кровати красный галстук, я сначала хотел завязать его вокруг предплечья, но тут же вспомнил, какие люди не столь давно (по меркам лагеря) ходили с красными повязками в том месте. В итоге завязал я его вокруг запястья левой руки."
    th "И так сойдёт."
    lur "Я, пожалуй, ограничусь галстуком, если вы не против."
    "Ольга оглянула мой внешний вид и хотела было возразить, но, вспомнив, каким убедительным я могу быть, согласилась."
    mt "Так уж и быть, хотя бы галстук носи. Не опоздай на линейку!"
    play sound sfx_drop_alisa_bag
    "Она бросила мне пакет с мыльно-рыльными принадлежностями и выбежала на улицу."
    hide mt with moveoutleft
    "Одевшись, я взглянул на себя в зеркало. На меня всё тем-же дьявольским взглядом оттуда смотрел рыжий красавчик. Поймал себя на мысли, что за прошлый день я привил себе всё больше черт его характера. Волосы были в идеальном состоянии, как и вчера."
    th "Ну хоть о чём-то тут волноваться не надо"
    stop ambience fadeout 2
    scene bg ext_house_of_mt_day with dissolve
    play music music_list["my_daily_life"] fadein 3
    play ambience ambience_camp_center_day fadein 1
    play sound sfx_close_door_1
    "Я захватил пакет и пошёл к умывальникам. На улице стоял прекрасный солнечный день. Жара не была столь испепеляющей и даже продувал прохладный бриз."
    scene bg ext_washstand_day with dissolve
    "Мне это пришлось очень даже по нраву."
    scene bg ext_washstand2_day with dspr
    play sound sfx_open_water_sink
    $ renpy.pause (1)
    play sound sfx_water_sink_stream
    "В умывальниках была знаменитая ледяная вода, но умыться мне это не помешало. "
    play sound sfx_water_splash
    "Закончив с водными процедурами, я забросил пакет в домик и направился на площадь. "
    window hide
    scene bg ext_house_of_mt_day with fade
    $ renpy.pause (1)
    scene bg ext_square_day with dissolve
    $ renpy.pause (1)
    show cg d2_lineup with dissolve
    window show
    "Людей там собралось уже много. Присоединившись к моему отряду, я встал и задумался о своём. Всё равно на этих линейках было больше слоганов, чем информации. Более того, если было что-то важное, то Ольга сама подойдёт к тебе после неё и убедится, что ты услышал."
    $ set_mode_nvl()
    "В основном я думал о событиях своего сна. Что это могло значить и кем был чёрный мужчина? \nЕсли пионер и правда нашёл себе какого-то дружка, который поможет ему отомстить, то будет ещё одна драка. \nБитвы я особо не боялся, учитывая свои новонайденные способности. Но почему-то битва именно с тем чёрным меня настораживала…"
    $ set_mode_adv()
    hide cg with dissolve
    show mt normal pioneer with dspr
    "Из раздумий меня вывела Ольга Дмитриевна, протянув мне какой-то листок. "
    mt "Вот обходной лист. Собери все подписи до конца дня."
    "Я, как можно более саркастично, отдал честь и выпалил"
    lur "Будет сделано!"
    "Ольга Дмитриевна раздраженно посмотрела на меня, но ничего не сказала и пошла по своим делам. "
    hide mt with moveoutleft
    th "Ну, раз нужно сделать, то нужно сделать. Может чего интересного подвернётся хоть в одной из локаций. Схожу на завтрак, а потом в обход"
    stop ambience fadeout 4
    stop music fadeout 4
    $ renpy.pause (1)
    scene bg int_dining_hall_people_day with fade
    play ambience ambience_dining_hall_full fadein 2
    play music music_list["went_fishing_caught_a_girl"] fadein 3
    "Войдя в столовую, я взял незамысловатый завтрак и пошёл на поиски столика. Свободные места оставались только с Леной и Славей.{w} Удивительно, как быстро эта столовая набивается голодными пионерами. "
    "Подойдя к столику, я спросил:"
    show sl normal pioneer close at left with dspr
    show un normal pioneer close at right with dspr
    lur "Многоуважаемые дамы, разрешите присесть?"
    show sl smile pioneer close with dspr
    sl "Конечно, садись!"
    "Лена лишь тихо кивнула, не устанавливая зрительный контакт."
    lur "Моё почтение."
    "Сказал я, после чего сел и принялся за еду. Между ложками густой каши я нашёл время спросить:"
    lur "Как дела у вас, девочки?"
    sl "У меня всё хорошо, сегодня помогаю вожатой проследить за младшим отрядом. "
    "Лена сначала скромно взглянула на меня, после чего сказала:"
    show un shy pioneer close at right with dspr
    un "Нормально."
    sl "Как твои? Занят чем-нибудь сегодня?"
    show un normal pioneer close at right with dspr
    lur "У меня тоже ничего. По плану лишь обходной, но я на большее и не рассчитываю."
    stop music fadeout 3
    play music music_list["you_lost_me"] fadein 3
    show sl serious pioneer close at left with dspr
    sl "Понятно. Слушай, а ты не видел случайно, кто вчера в столовую вломился? Там даже замок выбит совсем."
    lur "Возможно каким-то очень голодным пионерам до завтрака было невтерпёж. Унесли что-то важное?"
    "Лучший способ не врать при ответе на вопрос – не отвечать на сам вопрос, да и перевести тему заодно.{w} «The art of political doublespeak» - особенно полезно, когда тебе своя сущность запрещает лгать."
    sl "Да нет, ничего особенного. Лишь пара булок с кефиром. Но вор не должен оставаться безнаказанным! Даже за такую мелкую кражу. "
    show un serious pioneer close at right with dspr
    un "Может это была Алиса? Я вчера вечером её видела."
    th "Чего это ты её подставить решила? Нет уж, скинуть всё на Алису я не дам"
    lur "Вряд ли она сама бы смогла выломать замок, максимум взломала бы. А разбить его чем-то было бы слишком громко, кто-нибудь бы точно услышал."
    sl "Согласна, я бы услышала, если бы кто-то сломал его каким-то тупым предметом. Я недалеко на обходе была."
    th "Пронесло"
    "Тем временем я закончил есть."
    stop music fadeout 4
    lur "Ладно, девочки, я, пожалуй, пойду. Ещё обходной заполнять."
    stop ambience fadeout 2
    scene bg ext_dining_hall_near_day with dissolve
    play ambience ambience_camp_center_day fadein 2
    play sound sfx_alisa_lighter
    "Я попрощался с девочками и вышел из столовой. Выйдя на крыльцо, я отошёл чуть дальше скамейки и закурил очередную дорогую сигарету. "
    th "Стоило ли заступаться за Алису?{w} Несомненно, ведь я был там с ней и себя не выдал. Значит и её подставлять нельзя.{w} Может я это сделал не только ради себя? Да не, бред какой-то. Дьявол думает в первую очередь о себе и только о себе."
    "На этой мысли сигарета закончилась и пора было выдвигаться за подписями."
    "Куда бы пойти сначала?"
    stop ambience fadeout 2
    window hide
    $ disable_all_zones()
    $ set_zone ("clubs","dev_clubs_ru")
    $ set_zone ("music_club","dev_musical_ru")
    $ set_zone ("medic_house","dev_medical_ru")
    $ set_zone ("library","dev_library_ru")
    $ show_map ()
label dev_clubs_ru:
    window show
    scene ext_clubs_day with dspr
    play music music_list["smooth_machine"] fadein 4
    "Недолго думая, я решил пойти к зданию кружков."
    play sound sfx_open_door_clubs
    scene int_clubs_male_day with dissolve
    play ambience ambience_clubs_inside_day
    "Внутри меня ждали двое светил советской кибернетики.{w} Вернее, не ждали, вряд ли они хоть когда-то у себя кого-то ждут, но внутри кружка они определённо были.{w} Они напряжённо что-то мастерили у себя на столике, но отвлеклись на моё присутствие. "
    show el normal pioneer at right with dspr
    show sh normal pioneer at left with dspr
    el "О, привет. Ты, наверное, новенький?"
    lur "Доброго дня. Да, можно и так сказать. Мне бы вашу роспись для обходного."
    show el smile pioneer at right with dspr
    el "А ты запишись к нам в клуб, мы тебе её и поставим. "
    th "М-да, в клуб к этим неудачникам записываться я не хочу совершенно"
    el "Меня, кстати, Электроником зовут. А это Шурик."
    "Они оба протянули мне руки. Я пожал сначала руку Шурика и тут на меня нашла идея. "
    show sh surprise pioneer at fleft with dspr
    show el surprise close at center with dspr
    "Взяв руку, Электроника, я не отпустил её и сблизился с ним, установив близкий зрительный контакт."
    lur "Скажи мне, Электроник, моя запись в клуб — это ведь не то, чего ты больше всего хочешь, да?"
    "Электроник опешил и завороженно смотрел мне в глаза. Шурик же, недоумевая, наблюдал за происходящим со стороны."
    el "Н-нет…"
    play sound lu_desire
    lur "А чего же ты тогда {i}на самом деле{/i} желаешь?"
    el "Б-б… Быть с Женей.{w} Я хочу быть с Женей."
    show el upset pioneer close at center with dspr
    "С трудом выдал Электроник и сразу же покраснел. "
    if library_visited == False:
        lur "Хорошо. Давай тогда ты мне подпишешь обходной просто так, а я замолвлю за тебя словечко перед твоей дамой. Я как раз в библиотеку ещё должен зайти."
        "На моём лице расплылась недобрая улыбка."
        $ el_and_mz += 1
    else:
        th "Чёрт, я ведь сегодня с ней уже говорил. И не то, что бы мы сильно подружились при встрече. Обратно туда я не пойду."
        lur "Интересный факт... Было бы не хорошо, если бы об этом кто-то узнал."
        el "Чего ты хочешь?"
        lur "Что я {b}не{/b} хочу - это становиться частью вашего клуба. Подпиши лист и никто об этом не узнает. Даю слово."
    el "Х-хорошо. Давай сюда обходной."
    "Всё ещё малиновый Электроник выхватил у меня обходной, быстро расписался и отдал его мне."
    lur "Благодарю."
    scene ext_clubs_day with dissolve
    "Я забрал обходной и вышел на улицу."
    stop music fadeout 3
    stop ambience
    $ clubs_visited = True
    if library_visited == True and musical_visited == True and clubs_visited == True and medical_visited == True:
        jump locations_end
    window hide
    $ disable_current_zone ()
    $ show_map ()
label dev_musical_ru:
    window show
    scene bg ext_musclub_day with dspr
    "Я подошёл к музыкальному клубу и вошёл внутрь. "
    play sound sfx_open_door_2
    play music music_list["so_good_to_be_careless"] fadein 4
    play ambience ambience_music_club_day
    scene bg int_musclub_day with dissolve
    th "Странно, обычно я бы постучался. Кажется, с каждым днём я вживаюсь в роль всё больше"
    "Внутри я сначала никого не обнаружил, но потом я увидел, что кто-то копошится под роялем. "
    th "Пианино. Я бы не прочь опробовать дьявольское умение на нём играть. Этот обход становится всё интереснее"
    show cg d2_miku_piano2 with dissolve
    "Подойдя ближе, передо мной открылась довольно-таки пикантная картина пионерки, которая была под инструментом."
    th "Что же ты делаешь, сводный братик?"
    lur "Неплохой вид. Но я к вам не за этим, а за росписью."
    show cg d2_miku_piano with vpunch
    play sound sfx_piano_head_bump
    "Пионерка, похоже, даже не заметила, как я вошёл. От моих слов она попыталась вскочить, но ударилась головой о дно рояля. На вторую попытку она всё же выбралась."
    hide cg with dspr
    show mi smile pioneer with dspr
    mi "Ой, привет. А я тебя совсем не заметила. Ты же новенький? Меня зовут Мику. Я тебя вчера на площади видела, ты с Алисой курил. А ты знаешь, что курение плохо влияет на организм?"
    "На меня резко обрушился словесный поток изо рта этой девочки."
    th "Странно, её я среди толпы пионеров вчера не заметил. "
    lur "Воу, эй. Придержи коней. Да, новенький. Зовут Люцифер. Я тут с обходным пришёл, можешь расписаться?"
    mi "А, конечно, давай. Кстати, ты не хочешь в клуб к нам вступить? Тут, конечно, нас немного, но инструментов много, всем хватит. Ты на чём-нибудь играть умеешь? "
    lur "Да, умею. Хотел бы ваше фортепиано опробовать, если ты не против."
    mi "Нет, совсем не против, только за! Так тебя в клуб к нам записать?"
    lur "Эх, черт с ним. Ладно, записывай."
    hide mi with dspr
    show mi smile pioneer far at right with dspr
    "Я протянул ей обходной, а сам сел за рояль. Хрустнув пальцами, я положил их на клавиши."
    show lu_piano with dspr
    stop music fadeout 2
    th "Посмотрим, что эти дьявольские пальчики умеют"
    play music all_along_the_watchtower fadein 1
    "[Playing for Change - All Along the Watchtower (Lucifer cover)]"
    show mi surprise far at right with dspr
    "Спел я хорошо, да и сыграл отлично. Оказалось, я дьявол многих талантов."
    "Мику завороженно наблюдала за моей игрой и слушала песню."
    "[Нажми, чтобы закончить песню.]"
    hide lu_piano with dspr
    stop music fadeout 2
    show mi surprise at right with move
    mi "Ого, ты так классно играешь, а ещё и поёшь? Где ты этому научился? И песня на английском. Ты говоришь по-английски?"
    lur "Спасибо, я старался. Я говорю на всех языках. "
    mi "Что правда на всех? (На японском) А по-японски? "
    lur "(На японском) Конечно, все - значит все, без исключения. "
    "В жизни не говорил на японском, но теперь я точно убедился, что все возможные дьявольские таланты были мне доступны. "
    show mi happy at right with dspr
    mi "Потрясающе, не думала, что встречу кого-то, кто говорит на японском тут! Я так рада, что ты вступил в мой клуб!"
    "Мику чуть ли не прыгала от радости. В этот момент в клуб зашла Алиса."
    play sound sfx_close_door_1
    show mi smile at right with dspr
    show dv normal pioneer2 at left with dspr
    dv "Чем это вы тут заняты? "
    mi "О, Алиса, хорошо, что ты пришла!{w} Представляешь, Люцифер говорит на японском, а ещё играет на пианино и поёт отлично! "
    show dv smile pioneer2 at left with dspr
    dv "Что, правда умеет это всё? "
    mi "Да-да, Люцифер, сыграй нам ещё что-нибудь или спой!"
    lur "Что ж, ваше желание для меня закон. "
    play music shepard_tone
    lur "Это называется Тон Шепарда, по своей сути это просто повторяющаяся мелодия, но из-за её натуры, она звучит, как будто её тон бесконечно возрастает."
    "Теперь вместо одной завороженной девочки на меня смотрели две. Не то что бы я любил излишнее внимание, но это мне приходилось по нраву. "
    show mi happy at right with dspr
    show dv smile pioneer2 close at left with dspr
    stop music
    "Мику захлопала в ладоши. Алиса лишь улыбнулась и подошла поближе."
    dv "А ты и правда хорош. Оставил бы талантов остальным, а то так ничего не останется."
    lur "Хм, я уверен, что для некоторых, я оставил предостаточно. "
    "Я улыбнулся и взглянул на Алису, а потом на гитару, стоявшую у стены. "
    show dv surprise pioneer2 close at left with dspr
    $ renpy.pause (1)
    show dv smile pioneer2 close at left with dspr
    "Она поняла мой намёк, но всё же удивление промелькнуло у неё на лице. Ведь на её памяти я никогда не видел её с гитарой. Она наклонилась и прошептала мне на ухо:"
    dv "Приходи вечером на сцену. Покажу, на что способна."
    show mi normal pioneer at right with dspr
    mi "Ой, а чего это вы там шепчетесь?"
    play music music_list["awakening_power"] fadein 4
    show dv angry pioneer2 at left with dspr
    dv "Любопытной Варваре на базаре нос оторвали! Кстати, а ты починила мою гитару?"
    "Лицо Мику сменилось с любопытства на тихий ужас, который бывает, когда забываешь что-то важное и тебе об этом напоминают. Напоминают, но уже слишком поздно что-то исправить."
    show mi scared pioneer at right with dspr
    mi "Я-я хотела сегодня сделать, но там оказалось всё не так просто…"
    show dv rage pioneer2 at left with dspr
    dv "Ничего больше не хочу слышать. Чтобы гитару починила до завтра! "
    hide dv with moveoutright
    play sound sfx_slam_door_campus
    stop music fadeout 2
    "Она свирепо взглянула на Мику, после чего буквально вылетела из музыкального клуба и хлопнула дверью. Казалось, что ещё чуть-чуть и от ярости этой девушки здание бы рухнуло."
    show mi sad pioneer at right with dspr
    lur "Пожалуй я тоже пойду. Удачи, Мику."
    "Мику лишь грустно посмотрела мне вслед и пошла в сторону зачинщика этой словесной перепалки – к гитаре. "
    stop ambience
    play sound sfx_close_door_1
    play ambience ambience_camp_center_day
    play music i_tried_to_bring_it_back fadein 4
    scene ext_musclub_day with dissolve
    "Я вышел из кружка на улицу, где меня ждала Алиса."
    show dv sad pioneer2 with dissolve
    lur "Я так понимаю, что наш план на вечер отменяется?"
    "Алиса уже не злилась, но на лице была видимая грусть. "
    dv "Видимо да. Вечно эта Мику все планы портит."
    lur "Не злись ты так на неё. Ещё успеем на сцену сходить.{w} А вечером можем просто сходить погулять, как тебе такой вариант? "
    show dv normal pioneer2 with dissolve
    $ renpy.pause (1)
    show dv grin pioneer2 with dissolve
    "Алиса на секунду задумалась, после чего, явно с приподнятым настроением, сказала:"
    dv "Ну хорошо, раз уж ты настаиваешь."
    lur "Тогда встретимся после ужина. "
    dv "До вечера!"
    hide dv with moveoutright
    "Она побежала куда-то по своим делам, а я в то время направился дальше по обходному. "
    $ musical_visited = True
    stop music fadeout 4
    stop ambience fadeout 2
    if library_visited == True and musical_visited == True and clubs_visited == True and medical_visited == True:
        jump locations_end
    window hide
    $ disable_current_zone ()
    $ show_map ()
label dev_medical_ru:
    window show
    scene ext_aidpost_day with dspr
    "Следующей остановкой был медпункт. Я снова без стука вошёл внутрь."
    play sound sfx_open_door_2
    play ambience ambience_medstation_inside_day
    play music music_list["eternal_longing"] fadein 4
    scene int_aidpost_day with dissolve
    show cs smile
    "Медсестра, сидевшая за столом, развернулась. Представляла она из себя довольно фигуристую женщину с гетерохромией. "
    cs "Стучаться не учили, пионер? И чего без формы?"
    lur "И вам доброго дня. "
    "Съязвил я, после чего указал на галстук на запястье. "
    lur "Мы с Ольгой Дмитриевной пришли к определённой договорённости насчёт этого. Мне бы обходной подписать."
    cs "Хорошо, сейчас подпишем. Но для начала, раздевайся. "
    "Очевидно, она хотела ввести новенького пионера в ступор и смутить своей двусмысленностью, но не тут-то было."
    lur "Так уж и быть, хорошо. Но нам придётся это сделать быстро, мне ещё остальные подписи нужно собрать до обеда. "
    "Я начал медленно расстегивать рубашку. "
    cs "Погоди, пионер. Что сделать? "
    lur "Ну очевидно вы намекали на плотские угодья. Так что давайте быстро покончим с этим, раз вам неймётся, и я пойду дальше по своим делам. "
    show cg d5_cs with dissolve
    cs "Сначала осмотр, а потом посмотрим…"
    $ renpy.pause (1)
    hide cg with fade
    scene black with fade
    "Она приложила стетоскоп к моему оголенному торсу и принялась слушать. Ну или имитировать это. Я же принялся расстёгивать её халат. "
    play sound sfx_bed_squeak1
    "Вскоре медицинская койка зашаталась, как будто на ней пытаются вернуть к жизни дефибриллятором, но в десятикратной мощности. "
    scene int_aidpost_day with dissolve
    show cs smile close at right with dspr
    "Когда с грязным делом было покончено, я поцеловал медсестру напоследок и принялся одеваться. "
    cs "Мне кажется, я поняла, как вы пришли с Ольгой к «договорённости»."
    "Я лишь коварно улыбнулся в ответ. "
    lur "А теперь, подпишите обходной, пожалуйста. "
    "Я протянул ей листок и ручку со стола. Она быстро расписалась и снова разлеглась на койке. "
    "Получив свой квестовый предмет обратно, я направился к выходу."
    show cs smile far at right with dspr
    cs "Заходи почаще, Люцифер."
    lur "Обязательно."
    "Сверкнув ей глазами, я вышел из здания."
    $ medical_visited = True
    stop music fadeout 4
    stop ambience fadeout 2
    window hide
    if library_visited == True and musical_visited == True and clubs_visited == True and medical_visited == True:
        jump locations_end
    $ disable_current_zone ()
    $ show_map ()
label dev_library_ru:
    window show
    scene ext_library_day with dspr
    "Путь до библиотеки был ничем непримечателен."
    if el_and_mz == 1:
        "Разве что наполнен он был раздумьями о том, как свести местную библиотекаршу с Электроником. "
    scene int_library_day with dissolve
    play ambience ambience_library_day
    play music music_list["two_glasses_of_melancholy"]
    "Дойдя до пункта назначения, я вошёл, снова не стуча. К счастью, Женя спала за своим постом и язвительных комментариев об отсутствии стука не последовало."
    show cg d2_micu_lib with dissolve
    lur "Проснись и пой, пионерка! "
    "Саркастически сказал я, наклонившись поближе к спящей Жене. Она приподняла голову и сначала потупила на меня взгляд. Позже лицо приняло недовольный вид."
    hide cg with dspr
    show mz angry glasses pioneer close with dspr
    mz "Да не спала я. Можно было и не кричать. Чего тебе?"
    lur "Уж кто-то точно встал не с той ноги. Мне бы обходной подписать."
    mz "Давай сюда."
    "Грубо буркнув, она выхватила листок из моей руки."
    if el_and_mz == 1:
        lur "Может, если бы ты так на людей не срывалась, то твой кавалер бы набрался смелости подойти к тебе."
        show mz rage glasses pioneer with dspr
        mz "Какой ещё кавалер? Что за бред ты несёшь?"
        show mz normal glasses pioneer with dspr
        lur "Не притворяйся, что не замечаешь на себе внимание нашего светила советской науки. "
        mz "Ну, может замечала, что смотрит на меня Сергей как-то странно… Какое тебе вообще дело?"
        lur "Я лишь пытаюсь помочь нашему горе-Ромео. Дай ему шанс, видишь же, что сохнет по тебе парень."
        mz "Если я подумаю, то ты наконец уйдёшь отсюда?"
        lur "С радостью."
    else:
        "Она быстро расписалась на бумажке."
        lur "{i}Очень приятно{/i} было с вами пообщаться, но, к сожалению, у меня мало времени."
    "Я забрал у неё обходной и вышел из коммунистического логова самой доброжелательной пионерки лагеря. "
    play sound sfx_slam_door_campus
    stop ambience
    play ambience ambience_camp_center_day
    scene ext_library_day with dspr
    $ library_visited = True
    window hide
    stop music fadeout 4
    stop ambience fadeout 2
    if library_visited == True and musical_visited == True and clubs_visited == True and medical_visited == True:
        jump locations_end
    $ disable_current_zone ()
    $ show_map ()
label locations_end:
    window show
    play ambience ambience_camp_center_day
    play music music_list["my_daily_life"] fadein 4
    scene ext_house_of_mt_day with dspr
    "Пора бы отнести этот злосчастный обходной Оленьке и бежать на обед, время уже поджимает. Я направился к нашему домику. Дойдя до туда, внутри я никого не обнаружил. "
    th "Должно быть Ольга уже ушла на обед"
    play sound sfx_dinner_horn_processed fadein 1
    "В это время прозвучал горн, и я немедля пошёл в столовую."
    scene ext_dining_hall_away_day with dissolve
    "Дойдя до gourmèt a la Soviet, я увидел Ольгу на крыльце. Удачная встреча. Я подошёл к ней. "
    scene ext_dining_hall_near_day with dspr
    show mt smile pioneer with dspr
    lur "И снова здравствуйте. Я к вам с обходным. "
    mt "Привет, молодец. До конца дня можешь отдохнуть.{w} Дело сделал – гуляй смело! А теперь марш обедать. "
    stop ambience
    scene int_dining_hall_people_day with dspr
    play ambience ambience_dining_hall_full
    play sound sfx_open_door_1
    "На обед сегодня не давали ничего особенного: борщ и картошку с курицей. Прям первое, второе и компот. Только вместо компота был какой-то ягодный морс."
    "Свободное место оставалось только с будущими научными деятелями, так что к ним я и подсел. "
    show sh normal pioneer close at left with dspr
    show el normal pioneer close at right with dspr
    lur "Здравствия желаю, господа. Bon appetit."
    if el_and_mz == 1:
        el "Привет, привет. А что ты в конце сказал?"
        sh "Приятного аппетита, по-французски. "
        el "А ты откуда знаешь? "
    else:
        show el upset pioneer close at right with dspr
        "Элктроник взглянул на меня с кислой миной на лице и вернулся к потреблению еды."
        show el normal pioneer close at right with dspr
        sh "Снова привет. Клянусь, уже слышал где-то эту фразу."
        lur "Интересно, где же?"
    sh "Да в какой-то книжке прочитал, точно не помню. Вроде что-то из школьной программы."
    lur "Дайте угадаю, Война и Мир? "
    sh "Кажется она. Такая большая книга в нескольких томах. Никогда не любил художественную литературу. "
    lur "Это вы зря, господин. Литература делает человека культурным.{w} Какой прок от учёного, который общается, как сапожник?"
    el "Да ну эти ваши книжки, на практике всё гораздо интереснее. "
    sh "Далеко не всё можно исследовать эмпирическим путём. "
    lur "Почему же, можно. Но потребуется число попыток, стремящееся к бесконечности."
    "Это спровоцировало парней на обсуждение математики, но я их дальше особо не слушал. Я перестал прилагать усилия к ней в классе девятом, после чего технологии помогали мне удачно списывать. Тем временем я закончил есть."
    lur "Что ж, общение с вами невероятно интересное, но пожалуй я пойду. До встречи."
    stop ambience
    scene ext_dining_hall_near_day with dissolve
    play sound sfx_close_door_1
    "Я вышел из столовой. В этот раз идти гулять в лес мне особо не хотелось, хотя опасности там мне ничего не представляло. Я решил пойти на пристань. "
    stop music fadeout 4
    scene ext_boathouse_day with dissolve
    play ambience ambience_boat_station_day
    play music music_list["trapped_in_dreams"]
    play sound sfx_alisa_lighter
    "Дойдя до края пирса, я встал, глядя в сторону островов, и закурил. Я стоял в раздумьях о произошедшем за последние пару дней. Прошло менее 48 часов с приезда, но казалось, что прошло уже гораздо больше. Совершенно новая жизнь, новое тело, новые люди."
    "Может я и был с ними косвенно знаком через прохождение игры, но на деле они были для меня совсем другими, неизведанными. За эти два неполных дня произошло больше, чем за 2 месяца на карантине."
    th "Хоть Совёнок на него не закрыли и на том спасибо"
    "Я мог бы и дальше так спокойно наблюдать за волнами и думать, но тут я решил стряхнуть пепел с сигареты.{w} Слегка приударив большим пальцем конец фильтра, я стряхнул пепел и он полетел вниз. Полетел, но завис в воздухе. "
    stop ambience fadeout 2
    th "Что за..?"
    stop music fadeout 4
    play music music_list["just_think"]
    "Дым от сигареты тоже остался на месте, неподвижно, как и волны в реке. Я осмотрелся. Сзади меня на пристани стоял высокий чёрный мужчина в каком-то странном халате. "
    show amen with dspr
    dreamgirl "Твоё возвращение в Ад было запрошено. "
    lur "А ты должно быть…?"
    dreamgirl "Неужели за твоё время в изгнании ты смог забыть родного брата? Нет, ты бы никогда не забыл."
    "Фрагменты сериала промелькнули перед глазами. "
    th "Как же тебя звали? А… Ам… Точно! Аменадиэль"
    lur "Уже пошутить нельзя? Не помню, чтобы ты не любил моих шуток, Аменадиэль. "
    amenr "Может я и относился к ним снисходительно, но отец твоего юмора не оценит. Оставил свой трон в аду ради того, чтобы поиграть в пионеров здесь?"
    lur "Моя судьба всегда принадлежала мне и плевать я хотел на планы нашего отца. Если мне захочется, то останусь тут. "
    amenr "Считай себя предупреждённым, Люцифер. Отец не любит ждать. Надеюсь, нам не придётся снова тут встретиться. "
    hide amen with dspr
    play sound sfx_wind_gust
    "Из-за спины Аменадиэля раскрылись огромные серые крылья. Один взмах и его уже не было. Пепел от сигареты упал мне прямо на туфлю. "
    lur "Чёрт! Ах да, это же я. "
    "Я стряхнул пепел с туфли, выбросил сигарету в ближайший куст и направился куда подальше от этой пристани. "
    stop music fadeout 4
    "Скоротать время я решил у себя в домике. "
    scene ext_house_of_mt_day with dissolve
    "Я дошёл до него и лёг в шезлонг, стоявший у крыльца. По привычке я достал телефон, но не увидев ни иконки вайфая, ни 4G, на меня снова нахлынула реальность происходящего.{w} Интернета тут нет и не будет. Засунув телефон обратно в карман, я прикрыл глаза и задремал."
    show blink
    $ renpy.pause (1)
    $ persistent.sprite_time = 'sunset'
    $ sunset_time()
    "Меня разбудил легкий толчок в плечо." with hpunch
    show unblink
    scene ext_house_of_mt_sunset with dissolve
    show dv smile pioneer2 with dissolve
    play music music_list["timid_girl"] fadein 4
    play ambience ambience_camp_center_day
    dv "Чего это ты тут разлёгся?"
    lur "А что, нельзя? Это мой домик, в конце концов. "
    dv "Вставай, ужин пропустишь."
    lur "Что, уже? "
    play sound sfx_dinner_horn_processed
    "Ответил на мой вопрос голосящий по всему лагерю горн. "
    "Я потянулся, хрустнул спиной, встал и пошёл с Алисой на ужин. "
    scene ext_dining_hall_away_sunset with dissolve
    show dv smile pioneer2 at right with dspr
    dv "Не забыл, что у нас сегодня планы?"
    lur "Как же я мог забыть, если я инициатор? "
    dv "Да кто тебя знает, мог и просто так это сказать, чтобы утешить меня тогда."
    lur "Я ничего не говорю просто так. Если я пообещал тебе, что сегодня мы пойдём гулять, то сегодня мы пойдём гулять.{w} Я ведь дьявол своего слова."
    dv "Вот и хорошо. Только вот что ты заладил то, дьявол, дьявол. Притвориться больше некем?"
    lur "Так ты мне не веришь? Я и есть дьявол. Говорю я только правду. "
    "Алиса усмехнулась."
    show dv laugh pioneer2 at right with dspr
    dv "Ульянке свои байки рассказывай, может она поверит. А я без доказательств не клюну. "
    scene ext_dining_hall_near_sunset with dissolve
    show dv laugh pioneer2 at right with dspr
    "Тем временем мы дошли до столовой. "
    lur "Будут тебе доказательства. У нас полно времени после ужина. "
    dv "Заинтриговал, так заинтриговал. Пошли есть быстрее, там мест свободных сейчас не останется."
    stop music fadeout 4
    stop ambience fadeout 2
    play sound sfx_open_door_1
    scene int_dining_hall_people_day with dissolve
    play sound ambience_dining_hall_full
    show us normal pioneer far at left with dspr
    play music music_list["i_want_to_play"] fadein 4
    "Мы зашли в столовую, мест и правда оставалось немного. Из-за одного из столиков нам рукой махала Ульяна. Мы взяли гречку с котлетой и направились к ней. "
    show us normal pioneer close at left with dspr
    show dv normal pioneer2 close at right with dspr
    us "Приятного аппетита!"
    lur "Надеюсь, что жуков в этот раз не будет?"
    us "Так ты и в прошлые два раза мне помешал. Зачем мне снова пытать удачу?"
    lur "Дети часто делают нелогичные вещи."
    show us dontlike pioneer close at left with dspr
    show dv smile pioneer2 close at right with dspr
    "Ульяна лишь показала мне язык и продолжила есть. Алиса улыбнулась."
    "Она прошептала мне на ухо:"
    dv "Потом расскажешь мне, как ты про все её фокусы прознал."
    "Я взглянул ей в глаза, улыбнулся и кивнул. "
    stop music fadeout 4
    show el normal pioneer close at fright with dissolve
    "Ели мы не спеша, как я и любил. Вскоре люди постепенно начали уходить из столовой. Ульяна тоже быстро управилась и хотела уже убежать, как к нашему столику подошёл Электроник."
    el "Ребята, после ужина не расходитесь, я новую карточную игру придумал. Поиграем всем отрядом. "
    "Мы с Алисой переглянулись. Это значило, что наши планы на вечер либо снова рухнули, либо отсрочены на неопределённый срок.{w} Уйти ведь при всех мы не могли вместе. Я кивнул за нас Электронику в подтверждение."
    hide el with moveoutright
    "После ужина я решил выйти на крыльцо, подышать воздухом. На выходе меня встретил Электроник. "
    hide dv with dspr
    hide us with dspr
    show el smile pioneer at right with dissolve
    el "Слушай, а ты не можешь карты взять для нас? Мне Ольга Дмитриевна их не доверяет после последнего случая… "
    show mt normal pioneer at fleft with dspr
    show sl normal pioneer at left with dspr
    "Я собрался было возразить, ведь идти за картами, которые отсрочат нам прогулку, самому мне совсем не хотелось. Но тут как тут подошла Ольга Дмитриевна со Славей.  "
    el "Ольга Дмитриевна! Мы тут с Люцифером новую игру в карты придумали. Можно мы возьмём колоду, всем отрядом поиграем? "
    mt "Ну, раз Люцифер за, то пожалуй можно. Сходи, возьми их у меня в домике."
    "Мне даже слова вставить не дали, теперь не откажешься уже. "
    th "Когда это я стал доверенным лицом у вожатой?"
    lur "Славя, не составишь мне компанию?"
    sl "Конечно, пойдём. "
    play sound sfx_close_door_1
    scene bg ext_square_sunset with dissolve
    show sl normal pioneer at right with dspr
    play music music_list["get_to_know_me_better"]
    "Мы прошли примерно пол пути до домика Ольги Дмитриевны, как Славя сказала:"
    sl "Ой, а я вспомнила, что карты у меня в домике. В прошлый раз Ольга Дмитриевна забрала их у Сергея и отдала их мне. "
    th "Да ты что, а я уж думал до домика ОД придётся идти, чтобы ты вспомнила"
    lur "Хорошо, пойдём тогда к тебе."
    show sl shy pioneer at right with dspr
    $ renpy.pause (1)
    show sl normal pioneer at right with dspr
    "Лицо Слави на пару секунд покрылось лёгким румянцем. Двусмысленность фразы дала о себе знать."
    scene bg ext_houses_sunset with dissolve
    "Дальше мы пошли по направлению к её домику. Дойдя до места, Славя быстро заскочила к себе, не приглашая меня внутрь. Я дождался, пока она достала карты и вышла."
    lur "А я уж думал, что ты пригласишь меня внутрь. Ну ладно уж, пошли. "
    "Сказал я, лукаво улыбнувшись."
    show sl shy pioneer at right with dspr
    $ renpy.pause (1)
    show sl normal pioneer at right with dspr
    " Славя снова покраснела, после чего кратко произнесла:"
    sl "Пошли."
    stop music fadeout 4
    scene bg int_dining_hall_sunset with dissolve
    play music music_list["afterword"] fadein 4
    "Вскоре мы вернулись к столовой. Войдя внутрь, я увидел нарисованный плакат с сеткой участников нашего импровизированного турнира. "
    show cg lvl_1 with dissolve
    lur "Ну что, можем начинать? "
    hide cg with dspr
    show el normal pioneer with dspr
    el "Ещё не все собрались. Нет Жени."
    show el normal pioneer close with dspr
    "Я подошёл поближе к нему и заговорил с ним на пониженных тонах."
    lur "Так иди и позови её, Ромео. За одно и куда-нибудь на свидание позови. "
    show el grin pioneer close with dspr
    el "Я- я не могу. Я ведь организатор, мне нельзя уходить."
    if el_and_mz == 1:
        lur "Больно ты тут занят, всё уже готово ведь. Иди зови, я сегодня с ней говорил уже насчёт тебя."
        el "Правда? И как? Что она сказала?"
        lur "Что даст тебе шанс. Смотри не упусти. "
        el "Ладно, тогда я пошёл."
        hide el with moveoutright
        $ renpy.pause (1)
        show el smile pioneer at left with dissolve
        show mz normal pioneer at right with dissolve
        "Через несколько минут горе-романтик вернулся со своей возлюбленной. У него на лице читалась несказанная радость, хотя он и пытался её скрывать. Похожего энтузиазма у Жени не прослеживалось."
    else:
        show el normal pioneer with dspr
        lur "Больно ты тут занят, всё уже готово ведь. Иди позови её, сэкономишь нам обоим времени."
        el "Она уж точно не придёт, если позову её я. Иди лучше ты."
        lur "М-да, организатор из тебя лучше некуда. Ни карты достать, ни людей собрать не можешь. В чём тогда твоя роль во всём этом вообще?"
        show el angry pioneer with dspr
        el "Слушай, я понимаю, что вышло всё, скажем, не идеально. Просто сходи и позови её, пожалуйста."
        lur "Ладно, я сделаю это за тебя, но за тобой должок."
        el "Да, хорошо, главное приведи её."
        "Я уже собирался идти, когда через парадный вход вошла сама Женя, спасая меня от очередной прогулки."
        play sound sfx_close_door_1
        show mz normal pioneer at right with dissolve
        hide el with dspr
        show el surprise pioneer at left with dspr
        mz "Давайте уже покончим с этой игрой."
    el "Отлично, давайте начинать! Правила игры таковы:"
    "Дальше он зачитывал давно известные для меня правила игры, в которой ничего сложного не было. Я посмотрел на плакат. Первым моим противником была Лена. Я подошёл к её столику и присел напротив."
    hide el with dspr
    hide mz with dspr
    show un shy pioneer with dissolve
    lur "Удачи. "
    un "Спасибо. "
    scene black with dspr
    $ renpy.pause (1)
    scene bg int_dining_hall_sunset with dspr
    show un sad pioneer with dspr
    "Игра закончилась так же быстро, как и началась. Если тебе выпали хорошие комбинации, то можно было просто защищаться все два раунда и выиграть. "
    "Проиграв, Лена поникла. На лице виднелась её знаменитая вселенская грусть.{w} Может что-то в глубине души меня и коробило, но меня самого заставить грустить из-за проблем других было достаточно сложно. Особенно по таким незначительным причинам."
    lur "Ну не расстраивайся. Игроков восемь, а победитель то всего один. "
    hide un with moveoutleft
    stop music fadeout 4
    show cg lvl_2_semen_win with dissolve
    "Я вернулся к плакату, на котором говорилось, что следующий мой противник – Ульяна. Я сел к ней за столик."
    hide cg with dspr
    play music music_list["i_want_to_play"] fadein 3
    show us smile pioneer with dspr
    lur "Перед тем, как ты предложишь, нет, я не буду играть по другим правилам и нет, я не буду тебе поддаваться. Переигровок тоже не будет. Всё ясно?"
    "Я сверкнул глазами, от чего Ульяне явно стало не по себе."
    show us normal pioneer with dspr
    us "Да откуда ты- Ладно…"
    scene black with dspr
    $ renpy.pause (1)
    scene bg int_dining_hall_sunset with dspr
    show us dontlike pioneer with dspr
    "Победить Ульянку по обычным правилам не составило мне особого труда. Девочка же насупилась и выбежала из столовой. "
    stop music fadeout 3
    hide us with moveoutright
    "Следующим моим противником должна была быть Алиса. Я вернулся за столик, где мы недавно ужинали. Видимо она не сдвигалась с места."
    play music music_list["always_ready"] fadein 3
    show dv smile pioneer2 with dissolve
    lur "Вот и остались мы вдвоём. Уже подготовилась поздравлять меня с победой на нашей прогулке?"
    dv "Мы ещё посмотрим кто кого поздравлять будет. "
    "Игра началась. Алиса была более серьёзным оппонентом, чем предыдущие двое, но пара удачных карт и игра обернулась в мою сторону. В таких играх заход и правда решает всё. "
    th "Мои друзья где-то в реальном мире мне, наверное, сейчас кричат, что за такую удачу я мать продал или обожрался говна, или оба сразу"
    show dv angry pioneer2 with dspr
    dv "Везучий чёрт!"
    lur "Собственной персоной."
    stop music fadeout 3
    "Я игриво улыбнулся и взглянул на своего оппонента. Алиса, очевидно, была недовольна, похоже она хотела меня проучить.{w} Остальные ребята поздравили меня с победой, после чего я вышел на крыльцо и принялся ждать рыжую. "
    scene bg ext_dining_hall_near_sunset with dissolve
    "Пионеры постепенно начали расходиться. Долго себя ждать Алиса не заставила. "
    show dv normal pioneer with dspr
    lur "Ну что, пойдём?"
    dv "Пошли, чемпион."
    "Настроение у неё уже было получше."
    "Мы отошли от столовой в сторону площади. Как обычно бывает на прогулках, мы не задавали определённого направления, а просто шатались по лагерю куда глаза глядят. "
    $ persistent.sprite_time = 'night'
    $ night_time()
    scene bg ext_square_night with dissolve
    play sound ambience_camp_center_night
    play music music_list["dance_of_fireflies"]
    show dv normal pioneer with dspr
    dv "А я думала ты над Ульяной сжалишься, поддашься или поиграешь по её правилам. Маленькая ведь."
    lur "Никогда не любил людских отпрысков. Мелкие, надоедливые, громкие. Зачем мне такому поддаваться?"
    dv "Да, я детей тоже особо не люблю. Но Ульянка мне тут прям, как родственная душа стала.{w} Единственная понимает меня и поддержит со всего лагеря. "
    scene bg ext_playground_night with dissolve
    show dv normal pioneer with dspr
    lur "Я бы мог сказать, что теперь – не единственная. "
    show dv smile pioneer with dspr
    dv "А кто же ещё?"
    lur "А как ты думаешь?"
    dv "Нуу, даже не знаю."
    lur "Кто с тобой булки воровать ходил, на пианино для тебя играл. Кто гуляет то с тобой сейчас?"
    show dv grin pioneer with dspr
    dv "А правда ведь, кто? Ты вроде бы доказать мне что-то хотел. "
    lur "А, вот как. На слова мне не веришь?"
    dv "Что ты дьявол во плоти? Да кто в такое поверит?"
    lur "Ну хорошо, сейчас я тебе всё докажу."
    "Я остановился, она тоже. Я взглянул ей в глаза и промолвил:"
    show dv normal pioneer close with dspr
    lur "Скажи мне, Алиса, чего ты {i}на самом деле{/i} желаешь?"
    play sound lu_desire
    "Сначала мне показалось, что она сделала тот же загипнотизированный взгляд, что и все остальные, с кем я это проворачивал.{w} Но что-то пошло не так. Она попятилась назад от того, насколько я к ней приблизился. "
    show dv smile pioneer with dspr
    dv "Желаю? Чтобы ты мне уже показал наконец свои доказательства. Это что, было всё?"
    lur "Стой, как? На тебе не сработало? Ты должна была мне сказать свои самые заветные желания. "
    show dv laugh pioneer with dspr
    "Алиса рассмеялась."
    dv "Хах, так это фокус какой-то? Заставить меня говорить взглядом? Тебе нужно будет придумать что-то поубедительнее в следующий раз. "
    lur "Не понимаю, у тебя какой-то иммунитет. "
    dv "Да-да, ты просто ничего лучше не придумал, вот и оправдываешься. Пошли уже дальше, недодьявол. "
    "Мы пошли дальше, но я никак не мог понять, почему на неё не подействовала моя способность. Она работала во всех остальных случаях, на ком бы я не пробовал. "
    scene bg ext_library_night with dissolve
    show dv smile pioneer with dspr
    lur "Я найду способ тебе доказать, просто сегодня не вышло. "
    dv "Да брось ты, можешь притворяться сколько хочешь, так даже интереснее."
    dv "Приходи лучше завтра на сцену, если Мику гитару починит. А она её починит, если хочет остаться невредимой. "
    lur "Жестоко ты с ней. Я её даже на обеде сегодня не видел. Похоже, она сильно тебя боится. "
    dv "Я давно уже её просила починить эту гитару, но она вечно даёт пустые обещания, а потом забывает. Так не может продолжаться вечно."
    lur "Хорошо, я приду завтра. А сегодня уже поздновато стало. Пора бы нам расходиться, дабы не получить нагоняй от вожатой. "
    dv "Хорошая идея. "
    scene bg ext_square_night with dissolve
    show dv smile pioneer with dspr
    "Мы снова вернулись на площадь. "
    dv "Мне направо. "
    lur "Молодая леди не будет против, если некий очаровательный принц темноты проводит её до хором?"
    "Алиса усмехнулась. "
    show dv grin pioneer with dspr
    dv "Ну, раз уж вы так заговорили, то не против. "
    hide dv with dspr
    show dv grin pioneer close at right with dspr
    "Я поравнялся с ней и вытянул локоть в сторону, подавая довольно толстый намёк. Алиса сначала задумалась, но потом всё же обхватила меня за руку, и мы пошли в сторону её домика. "
    dv "Только не думай, что так будет всё время и что это что-то значит."
    lur "О, конечно, нет. Я просто провожу тебя до домика и на этом всё. "
    dv "Вот и правильно. "
    scene bg ext_house_of_dv_night with dissolve
    show dv smile pioneer with dspr
    "Вскоре мы добрались до домика. Алиса отпустила мою руку. "
    lur "Спокойной ночи."
    dv "Спокойной."
    lur "Может ещё поцелуйчик напоследок? "
    "Игриво спросил я, не ожидая ничего хорошего."
    dv "Ага, размечтался! Иди давай. "
    scene bg ext_house_of_mt_night with dspr
    "На этой нотке она зашла в домик, а я отправился к своему."
    stop music fadeout 4
    stop ambience fadeout 2
    scene bg int_house_of_mt_night with dspr
    play sound sfx_close_door_1
    "Внутри моего домика уже лежала вожатая и читала книгу. Я решил к ней больше не приставать, учитывая сложившуюся неловкую ситуацию. Поэтому я просто разделся, пожелал ей доброй ночи и лёг спать."
    show blink
    play music music_list["trapped_in_dreams"] 
    $ set_mode_nvl()
    "В этот раз перед сном мой разум наполнили мысли об Алисе. Теперь я с ней провёл достаточно много времени, да и она не против проводить его со мной. Похоже, она мне правда нравилась, даже со всеми своими колкостями. Возникла мысль и о том, что мои дьявольские чары на ней не сработали. Этот провал мог и неплохо подставить мой авторитет в её глазах. К счастью, она подумала, что я просто плохо пошутил. Похоже поживём – увидим, что будет дальше. Схожу завтра к ней на сцену и выясню не всё ли это просто так."
    "\nВстреча с «братом» мне тоже вспомнилась, но почему-то это меня волновало меньше, чем мои отношения с рыжей хулиганкой. Всё же, предупреждение о том, что отец-Бог хочет, чтобы я вернулся в ад, заставляет насторожиться. Только вот я не тот дьявол, которого они знают. Я просто оказался в его теле и воспоминаний о его прошлом у меня нет. Поэтому «вернуться» я физически не могу, если я там до этого не был. Конечно, вряд ли они силой смогут заставить меня туда отправиться, но чёрный ангел всё же может попытаться. Ладно, будь что будет. С каждым днём эти события становятся всё интереснее. "
    $ set_mode_adv()
    window hide
    jump devilishadv_day3_ru




























